var
  M,                    # Number of blocks
  N,                    # Number of observations
  X1[N], X2[N],         # Data
  Y[N],
  B[N],                 # Block
  e.B[M],               # Random effect
  beta, beta.1, beta.2, # Parameters
  tau, sigma,
  tau.B, sigma.B;       # Hyperparameters

model {
  # Likelihood
  for (i in 1:N) {
    Y[i] ~ dnorm(mu[i], tau)
    mu[i] <- beta + beta.1 * X1[i] +
                    beta.2 * X2[i] + e.B[B[i]]
  }

  # Prior of the random effect
  for (i in 1:M) {
    e.B[i] ~ dnorm(0, tau.B)
  }

  # Priors
  beta ~ dnorm(0, 1.0E-4)
  beta.1 ~ dnorm(0, 1.0E-4)
  beta.2 ~ dnorm(0, 1.0E-4)
  tau <- 1 / (sigma * sigma)
  tau.B <- 1 / (sigma.B * sigma.B)
  sigma ~ dunif(0, 1.0E+4)
  sigma.B ~ dunif(0, 1.0E+4)
}
